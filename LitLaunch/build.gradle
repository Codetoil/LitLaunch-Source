buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:2.0.2'
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'com.github.johnrengelman.shadow'

    group = project.litlaunchGroup
    version = project.litlaunchVersion

    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'

    repositories {
        mavenCentral()
    }

    dependencies {
        compileOnly 'com.google.code.gson:gson:2.8.6'
        compileOnly 'com.google.guava:guava:22.0'
        compileOnly 'net.sf.jopt-simple:jopt-simple:5.0.4'
    }

    if (!it.name.startsWith("litlaunch-common") && !it.name.startsWith("tpsmodsrc")) {
        configurations {
            // configuration that holds jars to include in the jar
            extraLibs
        }

        dependencies {
            testCompile project(":litlaunch-common")
            extraLibs project(":litlaunch-common")
            configurations.compile.extendsFrom(configurations.extraLibs)
        }

        jar {
            from {
                configurations.extraLibs.collect { it.isDirectory() ? it : zipTree(it) }
            }

        }
    }
}